
/-
Let X,Y be Banach spaces, and T âˆˆ B(X,Y). (bounded linear operators from X â†’ Y)
Given r>0, we denote X_r to be the closed ball of radius r in X

# Definition: T is compact if Cl(T(X_1)) is compact in Y

The theorem that we will prove is the equivalence of the following 
characterizations of compact operators on Banach Spaces
# The Following Are Equivalent
* 1) `T` is compact

* 2) `Cl(T(F))` is compact in `Y` for all bounded subsets `F` of `X`

* 3) If `{x_n}_{n=1}^âˆ` is a bounded sequence in `X`, 
then `{T(x_n)}_{n=1}^âˆ` has a convergent subsequence in `Y`

* 4) `T(X_1)` is totally bounded 
-/

import Mathlib.Analysis.LocallyConvex.Bounded
import Mathlib.Topology.Baire.CompleteMetrizable
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.GroupTheory.GroupAction.Basic

open Filter Metric Function Set Topology Bornology
open TopologicalSpace UniformSpace NormedSpace
open Classical Pointwise
open Uniformity
open ContinuousLinearMap

/-
Here we declare all the variables that we are going to use to define our statements
we want `ğ”½` and `ğ•‚` to be Normed fields 
now since they are not necesarily the same field in order to define linear maps between them
we need isomorphisms `Ïƒ` and `Ïƒ'` that are also isometries

Now we claim that `V` and `W` are Normed vector spaces over ğ”½ and ğ•‚ respectively
In order to define them as such lean needs to know that the additive structure on the V,W
are normed commutative groups

Finally `T` simply is a continuous linear map 
between these two modules that are Normed vector spaces
-/

variable {ğ•œ ğ”½ : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ”½] 
variable {Ïƒ' : ğ•œ â†’+* ğ”½} {Ïƒ : ğ”½ â†’+* ğ•œ} 
variable [RingHomInvPair Ïƒ Ïƒ'] [RingHomInvPair Ïƒ' Ïƒ] 
variable [RingHomIsometric Ïƒ] [RingHomIsometric Ïƒ']
variable {W : Type*} 
[NormedAddCommGroup W] 
[NormedSpace ğ•œ W] 
variable {V : Type*} 
[NormedAddCommGroup V] 
[NormedSpace ğ”½ V] 
variable (T : V â†’SL[Ïƒ] W) 

/-
We now write our 4 definitions of compactness
-/

-- Characterization 1) T is compact (Closure of the image of the unit ball is compact)
def PreCompactImageofUnitBall (T : V â†’ W) : Prop :=
  IsCompact (closure (image T (Metric.closedBall 0 1)))

-- Characterization 2) Cl(T(S)) is compact for all bounded F
def CompactImageofBounded (T : V â†’ W) : Prop :=
  âˆ€ S : Set V, IsBounded S â†’ IsCompact (closure (image T S))

-- Characterization 3) If {a_n}_{n=1}^âˆ is a bounded sequence in X, 
-- then {T(a_n)}_{n=1}^âˆ has a convergent subsequence in Y
-- having a convergent subsequence means that there is a limit point L
-- and a monotone function Ï† : â„• â†’ â„• that gives us the subsequence 
-- that converges (that is) Filter {T(a Ï† n)}_{n=1}^âˆ Tends to atTop L
def SeqCompactnessofBoundedSeq (T : V â†’ W) : Prop :=
  (a : â„• â†’ V) â†’ IsBounded (range a) â†’ âˆƒ L âˆˆ closure (range (T âˆ˜ a)), 
  âˆƒ (Ï† : â„• â†’ â„•), StrictMono Ï† âˆ§ Filter.Tendsto (T âˆ˜ a âˆ˜ Ï†) Filter.atTop (nhds L)

-- Characterization 4) T(X_1) is totally bounded 
def TotBoundedImageofUnitBall [Zero V] (T : V â†’ W) : Prop :=
  TotallyBounded (image T (Metric.closedBall 0 1))


/-
# We now prove the characterizations
The strategy to prove their equivalence will be to show
1 â†’ 2 â†’ 3 â†’ 4 â†’ 1
-/

/-
# Proof of 1) â†’ 2)
  Suppose F is bounded on X, then F âŠ† X_r for some r > 0
  Multiplication by non zero constants is a homeomorphism
  Hence Cl(T(F)) âŠ† Cl(T(X_r)) = Cl(rT(X_1)) = rCl(T(X_1))
  Last set is image of compact under a homeomorphism so it is compact
-/

theorem One_Implies_Two : PreCompactImageofUnitBall T â†’ CompactImageofBounded T := by 
  intro One
  -- We have : PreCompactImageofUnitBall â‡‘T as an assumption
  unfold CompactImageofBounded
  -- Our goals is:  âˆ€ (S : Set V), IsBounded S â†’ IsCompact (closure (â‡‘T '' S))
  intro S bS
  -- S : Set V , bS : IsBounded S
  rw [NormedSpace.isBounded_iff_subset_smul_ball ğ”½] at bS
  -- (S being bounded implies S is inside some scaled ball) âˆƒ a, S âŠ† a â€¢ ball 0 1 
  obtain âŸ¨a,hSâŸ© := bS 
  -- a : ğ”½ , S âŠ† a â€¢ ball 0 1

  -- Lem shows that : T (S) âŠ‚ (Ïƒ a) Cl (T V_1) (V_1 is the closed ball of V with radious 1)
  have Lem : (image T S) âŠ† (Ïƒ a) â€¢ (closure (image T (closedBall (0 : V) 1))) := by
    intro w hw 
    obtain âŸ¨v,âŸ¨hv,heqâŸ©âŸ© := hw
    obtain âŸ¨v',âŸ¨hu,hUâŸ©âŸ© := mem_of_mem_of_subset hv hS
    -- So far we have w âˆˆ T(S) , w=T(v) , v = a v' , v' âˆˆ V_1   
    let Hv : a â€¢ v' = v := hU    
    rw [â† Hv] at heq 
    refine âŸ¨?_,âŸ¨?_,?_âŸ©âŸ©
    -- w âˆˆ Ïƒ a â€¢ closure (â‡‘T '' closedBall 0 1) iff the following 3 conditions are satified
    -- 1) there is a w' âˆˆ W (we use w'=T v')
    Â· exact T v'
    -- 2) w' =T v' âˆˆ closure (â‡‘T '' closedBall 0 1)
    Â· apply mem_of_mem_of_subset (mem_image_of_mem (â‡‘T) hu) 
      -- using the fact that (â‡‘T '' closedBall 0 1) âŠ† closure (â‡‘T '' closedBall 0 1)
      let balls : â‡‘T '' ball 0 1 âŠ† â‡‘T '' closedBall 0 1 := by
        apply Set.image_subset
        exact ball_subset_closedBall
      exact balls.trans subset_closure
    -- 3) w = (Ïƒ a) T v' = (Ïƒ a) T v'
    Â· let betaReduc : (fun x â†¦ Ïƒ a â€¢ x) (T v') = (Ïƒ a) â€¢ (T v') := rfl  
      rw [â† heq,betaReduc]
      apply Eq.symm
      exact (ContinuousLinearMap.map_smulâ‚›â‚— T a v') 
  -- We now use the fact that closed subsets of compact sets are compact
  rw [â† exists_isCompact_superset_iff] 
  -- So we want to find âˆƒ K, IsCompact K âˆ§ â‡‘T '' S âŠ† K
  -- Here we use the same set as in our Lemma Lem
  use (Ïƒ a) â€¢ closure (image T (closedBall (0 : V) 1))
  refine âŸ¨?_,LemâŸ©
  -- By continuity of scalar multiplication K being compact implies (Ïƒ a) K is compact 
  apply IsCompact.smul
  exact One 
  
 
/-
# Proof of 2) â†’ 3)
  If F={x_n}_{n=1}^âˆ is bounded then by assumption Cl(T(F)) is compact
  Hence by characterization of compactness on metric space it has a convergent subsequence
  So for any subsequence in T(F) âŠ† Cl(T(F)) also has a convergent subsequence
-/
theorem Two_Implies_Three : CompactImageofBounded T â†’ SeqCompactnessofBoundedSeq T := by 
  intro Two 
  unfold CompactImageofBounded at Two
  -- We start with our assumption that: CompactImageofBounded T
  unfold SeqCompactnessofBoundedSeq
  intro a Boundseq
  -- We introduce a bounded sequence {a(n)}_{n=1}^âˆ âˆˆ V 
  -- Our goal is to show that âˆƒ L âˆˆ Closure ({T a(n)}) such that 
  -- this L is the limit of some subsequence {T a(Ï† n)}

  -- Characterization 2 tells us that Closure ({T a(n)}) is compact since {T a(n)} is bounded
  have compactRange : IsCompact (closure (â‡‘T '' (range a))) := by 
    exact Two (range a) Boundseq 
  -- On metric spaces compact â†” sequentially compact 
  -- Hence we have that Closure ({T a(n)}) is sequentially compact
  have seqCompactRange : IsSeqCompact (closure (â‡‘T '' (range a))) := by 
    apply IsCompact.isSeqCompact
    exact compactRange 
  unfold IsSeqCompact at seqCompactRange

  let x := T âˆ˜ a
  -- The key to solving our goal is to first show that
  -- x n = T (a n) âˆˆ closure (â‡‘T '' range a))
  have key : (âˆ€ (n : â„•), x n âˆˆ closure (â‡‘T '' range a)) := by 
    intro n 
    -- by construction we already have x n = T (a n) âˆˆ (â‡‘T '' range a))
    have InRan : x n âˆˆ (â‡‘T '' range a) := by
      have Hxn : x n = (T âˆ˜ a) n := by 
        exact rfl
      rw [Hxn]
      rw [mem_image]
      use (a n)
      refine âŸ¨?_,?_âŸ©
      exact mem_range_self n
      exact Hxn
    -- so we use the standard tactic that (â‡‘T '' range a)) âŠ† closure (â‡‘T '' range a))
    apply mem_of_mem_of_subset InRan
    exact subset_closure
  
  -- this is a trivial small lemma to rewrite our goal into something easier to work with
  have rangeLemma : range (â‡‘T âˆ˜ a) = â‡‘T '' range a := by
    exact range_comp (â‡‘T) a
  rw [rangeLemma]
  -- Now the goal changes where L lives into
  -- âˆƒ L âˆˆ closure (â‡‘T '' range a), (T âˆ˜ a âˆ˜ Ï†) Tends to L

  -- Remember when we used our characterization 2 to prove the range was sequentially compact?
  -- well we use it now
  exact seqCompactRange key
   
/-
# Proof of 3) â†’ 4)
  We will show Cl(T(X_1)) is compact. 
-/

theorem Three_Implies_Four [CompleteSpace V] [CompleteSpace W] : 
SeqCompactnessofBoundedSeq T â†’ TotBoundedImageofUnitBall T := by 
  intro three 
  unfold SeqCompactnessofBoundedSeq at three 
  unfold TotBoundedImageofUnitBall 
  -- Our goal is to show that T (V_1) is totally bounded (V_1 is the closed unit ball of V)
  -- Now T (V_1) is totally bounded if closure T (V_1) is
  apply TotallyBounded.subset subset_closure
  -- We also know that in metric spaces if you are sequentially compact then you are totally bounded
  -- So lets change our goal into showing that closure T (V_1) is sequentially compact
  apply IsSeqCompact.totallyBounded
  unfold IsSeqCompact 
  -- So we introduce a sequence {y(n)} âˆˆ closure T (V_1)
  intro y Hy 
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  /-
    Since each y(n) âˆˆ Closure T (V_1) then there must exists some x(n) such that
    y(n) and T(x(n)) are very close (in particular we want them at distance 1/(n+1))

    In order to show this theorem we translate convergence from the world of filters into
    Îµ,Î´ world and crawl our way to get this nice bound

    By definition of the closure we should be able to find at least one x(n) with this property
    However since it is not unique I decided to use axiom of choice to make a sequence out of them
  -/
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  have ExistsX : âˆƒ x : â„• â†’ V , âˆ€ n, 
  (x n âˆˆ closedBall 0 1 âˆ§ â€–(y n) -  T (x n)â€– â‰¤ (1/(n+1)) ):= by 
    -- We localize our question, and show x(n) exists for each (n : â„•)
    have forallExists : âˆ€ (n : â„•) , 
    âˆƒ xN âˆˆ (closedBall (0 : V) 1), â€–(y n) -  T (xN)â€– â‰¤ (1/(n+1)) := by 
      intro n 
      have Hyn : y n âˆˆ closure (â‡‘T '' closedBall 0 1) := by 
        apply Hy 
      rw [SeminormedAddCommGroup.mem_closure_iff] at Hyn
      have helperLemma : âˆƒ b âˆˆ â‡‘T '' closedBall 0 1, â€–y n - bâ€– â‰¤ (1/(n+1)) := by 
        obtain âŸ¨b,âŸ¨bInSet,LEâŸ©âŸ© := Hyn (1/(n+1)) Nat.one_div_pos_of_nat
        refine âŸ¨b,âŸ¨bInSet,le_of_lt LEâŸ©âŸ©
      obtain âŸ¨Txn,âŸ¨âŸ¨v,âŸ¨vInBall,TvEqTxnâŸ©âŸ©,ineqâŸ©âŸ© := helperLemma  
      use v 
      refine âŸ¨vInBall,?_âŸ©
      rw [TvEqTxn]
      exact ineq 
    --Now that we know they exists we construct the sequence
    apply Classical.axiomOfChoice forallExists -- Here we use choice
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  obtain âŸ¨x,closeConditionâŸ© := ExistsX
  -- We want to show that {x(n)} is a bounded sequence 
  have Bx : IsBounded (range x) := by 
    -- By construction of x(n) we know range x âˆˆ  (closedBall (0:V) 1) 
    have subsetofBall : (range x) âŠ† (closedBall (0:V) 1) := by 
      intro xn hxn 
      obtain âŸ¨n,HxnâŸ© := hxn
      rw [â† Hxn]
      obtain âŸ¨xnInball,_âŸ© := closeCondition n 
      exact xnInball
    -- We know V_1 is bounded
    have ballIsbounded : IsBounded (closedBall (0: V) 1) := by 
      exact isBounded_closedBall
    -- Hence a subset of it will also be bounded
    exact IsBounded.subset ballIsbounded subsetofBall
  
  -- Here is where we use Characterization 3
  -- We use to find a convergent subsequence of {x(n)}
  --Namely x(Ï† n) â†’ L
  obtain âŸ¨L,âŸ¨hL,âŸ¨Ï†,âŸ¨phiMono,convergesâŸ©âŸ©âŸ©âŸ© := three x Bx

  -- The idea now is that since y(n) and x(n) are so close
  -- then L being a limit of x(Ï† n) must also be the limit of y(Ï† n)
  -- So it would be the limit of some subsequence of {y(n)}
  use L
  -- Small lemma to rewrite our goal
  -- this says that closure {T (x n)} âŠ† closure T (closedBall 0 1)
  have setLemma : closure (range (â‡‘T âˆ˜ x)) âŠ† closure (â‡‘T '' (closedBall 0 1)) := by 
    -- If {T (x n)} âŠ† T (closedBall 0 1) we would be done, so we show this
    -- which is true from construction of {x(n)}
    have helper : range (â‡‘T âˆ˜ x) âŠ† â‡‘T '' (closedBall 0 1):= by
      intro txa Htxa 
      obtain âŸ¨k,hEqâŸ© := Htxa
      rw [â† hEq] 
      apply mem_image_of_mem T
      obtain âŸ¨xkInball,_âŸ© := closeCondition k
      exact xkInball
    -- We now use monotonicity of the closure
    apply closure_mono helper 
  refine âŸ¨?_,âŸ¨Ï†,âŸ¨phiMono,?_âŸ©âŸ©âŸ©
  Â· apply mem_of_mem_of_subset hL 
    exact setLemma
  -- OUR goal is now reduced to showing that Tendsto (y âˆ˜ Ï†) atTop (ğ“ L) !!
  -- Here is where we travel to Îµ,Î´ world again.
  rw [NormedAddCommGroup.tendsto_atTop']
  intro Îµ nne 
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  /- 
    Since we want to show that âˆƒ N, âˆ€ (n : â„•), N < n â†’ â€–(y âˆ˜ Ï†) n - Lâ€– < Îµ
    then we will use the classic triangle inequality technique 

    1) â€–(y âˆ˜ Ï†) n - Lâ€– â‰¤ â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– + â€–T ((x âˆ˜ Ï†) n) - Lâ€–
    2) â€–T ((x âˆ˜ Ï†) n) - Lâ€– < Îµ/2
    3) â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– â‰¤ Îµ/2

    Hence we prove these 3 lemmas
    the idea is that our goal should follow easily from this
  -/
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  -- 1) Triangle inequality
  have LemmaOne : âˆ€ (n : â„•) , 
  â€–(y âˆ˜ Ï†) n - Lâ€– â‰¤ â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– + â€–T ((x âˆ˜ Ï†) n) - Lâ€– := by 
    exact fun n â†¦ norm_sub_le_norm_sub_add_norm_sub ((y âˆ˜ Ï†) n) (T ((x âˆ˜ Ï†) n)) L
  -- 2) Second summand of triangle inequality < Îµ/2
  -- We have shown that Tendsto (â‡‘T âˆ˜ x âˆ˜ Ï†) atTop (ğ“ L) 
  -- hence translating to Îµ,Î´ world gives us our result
  have LemmaTwo : âˆƒ N : â„• , âˆ€ (n : â„•), (N < n) â†’ â€–T ((x âˆ˜ Ï†) n) - Lâ€– < (Îµ/2) := by 
    rw [NormedAddCommGroup.tendsto_atTop'] at converges 
    have hnne : 0 < Îµ/2 := by 
      exact half_pos nne
    obtain âŸ¨N,HNâŸ© := converges (Îµ/2) hnne 
    use N
    intro n hn 
    apply HN n hn 
  -- 3) First summand of triangle ineq â‰¤ Îµ/2
  -- We prove the following inequalities in order to show our goal
  -- â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– â‰¤ 1/(n' + 1) â‰¤ 1/(M+1) â‰¤  Îµ/2
  have LemmaThree : âˆƒ M : â„• , âˆ€ (n : â„•), (M < n) â†’ â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– â‰¤ (Îµ/2) := by 
    -- These lines are only saying that we can find and integer M such that 
    -- 1/(M+1) â‰¤ Îµ/2
    have hnne : 0 < Îµ/2 := by 
      exact half_pos nne
    let M : â„• := Nat.ceil (2/Îµ)
    have lessThan1 : (1/(M+1)) â‰¤ (Îµ/2) := by 
      have arith : 1/(2/Îµ) = (Îµ/2) := one_div_div 2 Îµ
      rw [â† arith,one_div_le_one_div]
      Â· apply le_trans (Nat.le_ceil (2 / Îµ)) 
        have H : âŒˆ2 / ÎµâŒ‰â‚Š â‰¤ M := Nat.le_refl âŒˆ2 / ÎµâŒ‰â‚Š
        apply le_trans (Nat.cast_le.mpr H)
        apply le_add_of_le_of_nonneg (le_refl (M : â„))
        exact zero_le_one' â„
      Â· exact Nat.cast_add_one_pos M 
      Â· have arith2 : (2/Îµ) = 1/(Îµ/2) := Eq.symm (one_div_div Îµ 2)
        rw [arith2,one_div_pos]
        exact hnne

    use M 
    intro n hn 
    let n' : â„• := Ï† n 
    -- Since Ï† is a monotone function then M < Ï†(n)=n'
    have hn' : M < n' := by 
      apply lt_of_lt_of_le hn 
      apply StrictMono.id_le phiMono 
    -- We have â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– â‰¤ 1/((Ï† n) +1) = 1/(n' + 1) from construction of {x(n)}
    have lessThan2 : â€–(y âˆ˜ Ï†) n  - T (x (Ï† n))â€– â‰¤ 1 / (n' + 1) := by 
      obtain âŸ¨_,HâŸ© := closeCondition n' 
      exact H
    -- Since M < Ï†(n) = n' then 1/(n'+1) â‰¤ 1/(M+1)
    have lessThan3 : 1/((n'+1) : â„) â‰¤ 1/((M+1) : â„) := by 
      have nzR : 0 < ((M + 1) : â„) := Nat.cast_add_one_pos M
      apply one_div_le_one_div_of_le nzR
      have Mltn : (M : â„) â‰¤ (n' : â„) := by 
        have IntLt : M â‰¤ n' := le_of_lt hn' 
        exact Nat.cast_le.mpr IntLt -- (Note*) This is interesting, it means I can cast inequlities 
      exact add_le_add_right Mltn 1
    -- We now have As we wanted
    -- â€–((y âˆ˜ Ï†) n) - T ((x âˆ˜ Ï†) n)â€– â‰¤ 1/(n' + 1) â‰¤ 1/(M+1) â‰¤  Îµ/2
    apply le_trans (le_trans lessThan2 lessThan3)
    exact lessThan1
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------  
    -- We can now collect our result!
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  obtain âŸ¨N,twoâŸ© := LemmaTwo
  obtain âŸ¨M,threeâŸ© := LemmaThree
  use (N+M) 
  intro n hn 
  apply lt_of_le_of_lt
  Â· apply LemmaOne 
  have IneqR : â€–T ((x âˆ˜ Ï†) n) - Lâ€– < Îµ / 2 := by 
    have nBig : N < n := by 
      exact Nat.lt_of_add_right_lt hn
    apply two n nBig 
  have IneqL : â€–(y âˆ˜ Ï†) n - T ((x âˆ˜ Ï†) n)â€– â‰¤ Îµ / 2 := by 
    have nBig : M < n := by 
      rw [add_comm] at hn
      exact Nat.lt_of_add_right_lt hn
    apply three n nBig 
  have arith : Îµ = (Îµ/2) + (Îµ/2) := Eq.symm (add_halves Îµ)
  rw [arith]
  apply add_lt_add_of_le_of_lt IneqL IneqR
  ---------------------------------------------------------------------------------
  ---------------------------------------------------------------------------------
  

/-
# Proof of 4) â†’ 1)
  By characterization of compactness in metric spaces it suffices to show
  Cl(T(X_1)) is complete and totally bounded, 
  however it is already complete since it is a closed subset of a Banach space
  It is totally bounded by assumption so we are done!
-/
theorem Four_Implies_One [CompleteSpace V] [CompleteSpace W] : 
TotBoundedImageofUnitBall T â†’ PreCompactImageofUnitBall T := by 
  intro Four 
  unfold PreCompactImageofUnitBall 
  unfold TotBoundedImageofUnitBall at Four
  rw [isCompact_iff_totallyBounded_isComplete]
  refine âŸ¨?_,?_âŸ©
  Â· apply TotallyBounded.closure
    exact Four 
  apply IsClosed.isComplete
  exact isClosed_closure

-- I used https://leansearch.net/ to find some names of theorems, this is a great website
